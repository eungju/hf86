\
\ LOG.F
\   Capture screen output in a textfile for hForth.
\
\ 1996. 2. 28.
\ Wonyong Koh.
\
\ Usage:
\   LOGON  ( -- )
\	화면 출력을 HFORTH.LOG에 갈무리합니다. 출력 가리킴실행값 'emit이
\	바뀐 후에는 (예를 들어, HIOMULT?.F의 TEXT나 HGRAPHIC을 시킨 후나
\	포스 시스템이 '?'을 보여 잘못을 알린 경우) LOGON이라고 다시 시켜야
\	갈무리가 계속됩니다.
\	Start to save screen output in HFORTH.LOG.
\	Please reissue 'LOGON' after changing "'emit" vector.
\	 (for example, after TEXT or HGRAPHIC of HIOMULT?.F)
\   LOGOFF  ( -- )
\	HFORTH.LOG를 닫고 화면 갈무리를 멈춥니다.
\	Close HFORTH.LOG and stop saving screen output.

MARKER ~LOG

BASE @
GET-ORDER  GET-CURRENT

GET-ORDER DOS-WORDLIST SWAP 1+ SET-ORDER
DOS-WORDLIST SET-CURRENT

HEX
8000 CONSTANT invalid-fid

invalid-fid VALUE logfid
0 VALUE old'emit
CREATE LogBUFFER 1 CHARS ALLOT ALIGN

: LogEMIT  ( char -- )
    DUP LogBUFFER C! LogBUFFER 1 CHARS logfid WRITE-FILE THROW
    old'emit EXECUTE ;

NONSTANDARD-WORDLIST SET-CURRENT

: LOGON
    logfid CLOSE-FILE DROP
    S" HFORTH.LOG" W/O OPEN-FILE
    ?DUP IF
	DUP [ 2 iorOffset + ] LITERAL <>	\ file not found?
	IF THROW THEN
	2DROP S" HFORTH.LOG" W/O CREATE-FILE THROW TO logfid
    ELSE
	TO logfid
	logfid FILE-SIZE THROW logfid REPOSITION-FILE THROW
    THEN
    CR ." All characters on screen will be saved in HFORTH.LOG until 'emit is revectored."
    CR ." 화면에 보이는 글자들은 모두 'emit이 바뀌기 전까지 HFORTH.LOG에 갈무리됩니다." CR
    'emit ['] LogEMIT <> IF
	'emit TO old'emit
	['] LogEMIT TO 'emit
    THEN ;

: LOGOFF
    logfid CLOSE-FILE
    invalid-fid TO logfid
    old'emit TO 'emit ;

LOGON

SET-CURRENT  SET-ORDER
BASE !

CHAR " PARSE FILE" ENVIRONMENT?
[IF]
  0= [IF] << CON [THEN]
[ELSE] << CON
[THEN]
